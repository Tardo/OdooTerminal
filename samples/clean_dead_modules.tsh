// min-v 11.5.0; group system
// Removes modules that doesn't exists anymore...
// WARNING: This is destructive!! Installed modules can't be removed, but act prudently.

$mods = (search ir.module.module -f icon -d [['state', '=', 'uninstalled']])
$dmods = (arr_filter $mods function(item) { 
    $ficon = (fetch -u $item['icon'] -o {method: 'HEAD'})
    return ($ficon['status'] != 200)
})
$dmods_len = $dmods['length']
if ($dmods_len == 0) {
    print "Nothing to do :)"
    return -1
}
print "Modules to remove:"
print $dmods
$do = (input -q 'Remove ' + $dmods_len + ' modules?' -c ['y','n'] -d 'n')
if ($do == 'y') {
    $rids = (arr_map $dmods function(item) { return $item['id'] })
    $fails = []
    for ($i = 0; $i < $dmods_len; $i += 1) {
        $rid = $rids[$i]
        $res = (unlink -m ir.module.module -i $rid)
        if (!$res) {
            arr_append $fails $rid
        }
    }
    $fails_count = $fails['length']
    print ($dmods_len - $fails_count) + ' of ' + $dmods_len + ' modules deleted!'
    if ($fails_count != 0) {
        print "Errors:"
        print $fails
    }
} else {
    print "Aborted!"
}