// Conway's Game of Life

function get_cell_state(grid, width, height, index) {
  $cx = (fixed ($index % $width))
  $cy = (fixed ($index / $width))

  $pos_check = [[-1, -1], [0, -1], [1, -1], [1, 0], [1, 1], [0, 1], [-1, 1], [-1, 0]]
  $alive_count = 0
  for ($o = 0; $o < $pos_check['length']; $o = $o + 1) {
    $nx = $cx + $pos_check[$o][0]
    $ny = $cy + $pos_check[$o][1]
    if ($nx < 0 || $nx >= $width || $ny < 0 || $ny >= $height) {
        continue
    }
    $npos = $width * $ny + $nx
    if ($grid[$npos] == 1) {
      $alive_count = $alive_count + 1
    }
  }

  $nstate = 0
  if ($grid[$index] == 0 && $alive_count == 3) {
    $nstate = 1
  }
  if ($grid[$index] == 1 && ($alive_count > 3 || $alive_count <= 1)) {
    $nstate = 0
  }
  if ($grid[$index] == 1 && ($alive_count == 2 || $alive_count == 3)) {
    $nstate = 1
  }
  return $nstate
}

function get_new_grid_state(grid, width, height, cell_size) {
  $cgrid = []
  for ($e = 0; $e < $grid['length']; $e = $e + 1) {
    arr_append $cgrid (get_cell_state $grid $width $height $e)
  }
  return $cgrid
}

function draw_grid_state(grid, width, cell_size) {
  for ($e = 0; $e < $grid['length']; $e = $e + 1) {
    if ($grid[$e] == 0) {
        continue
    }
    $x = (fixed ($e % $width)) * $cell_size
    $y = (fixed ($e / $width)) * $cell_size
    2d_rect -c $win -x $x -y $y -w $cell_size -h $cell_size -rc '#0000ff'
  }
}

$win = (2d_create_window -w 400 -h 400 -x 600 -y 120)
$CELL_S = 16
$NUM_CELLS_ROW = (fixed $win['width'] / $CELL_S)
$NUM_CELLS_COL = (fixed $win['height'] / $CELL_S)
$TOTAL_CELLS = $NUM_CELLS_ROW * $NUM_CELLS_COL
$ITERATIONS = 150

$grid = []
// Populate Grid
for ($i=0; $i<$TOTAL_CELLS; $i=$i+1) {
  arr_append $grid (gen int 0 1)
}

for ($i = 0; $i < $ITERATIONS; $i = $i + 1) {
  2d_clear -c $win
  draw_grid_state $grid $NUM_CELLS_ROW $CELL_S
  $grid = (get_new_grid_state $grid $NUM_CELLS_ROW $NUM_CELLS_COL $CELL_S)
  sleep 1
}

2d_destroy_window $win